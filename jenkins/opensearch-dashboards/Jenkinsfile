pipeline {
    agent none
    triggers {
      parameterizedCron '''
        H */2 * * * %INPUT_MANIFEST=1.1.0/opensearch-dashboards-1.1.0.yml
      '''
    }
    stages {
        stage('parameters') {
            steps {
                script {
                    properties([
                            parameters([
                                    string(
                                            defaultValue: '',
                                            name: 'INPUT_MANIFEST',
                                            trim: true
                                    )
                            ])
                    ])
                }
            }
        }
        stage('detect Docker image + args to use for the build') {
            steps {
                script { 
                    manifest = readYaml(file: "manifests/$INPUT_MANIFEST") 
                    
                    dockerImage = "${manifest.ci?.image?.name}" 
                    // If the 'image' key is not present, it is populated with "null" string 
                    if (dockerImage == null || dockerImage == "null") {
                        error("The Docker image for the build is required but was not provided in the manifest")
                    }

                    dockerArgs = "${manifest.ci?.image?.args}" 
                    // If the 'args' key is not present, it is populated with "null" string 
                    if (dockerArgs == null || dockerArgs == "null") {
                        dockerArgs = ''
                    }

                    echo "Using Docker image: " + dockerImage
                    echo "Using Docker container args: " + dockerArgs
                }
            }
        }
        stage('build') {
            parallel {
                stage('build-x64') {
                    agent {
                        docker {
                            label 'Jenkins-Agent-al2-x64-c54xlarge-Docker-Host'
                            image dockerImage
                            args dockerArgs
                            alwaysPull true
                        }
                    }
                    steps {
                        script {
                            build()
                        }
                    }
                    post() {
                        always {
                            cleanWs disableDeferredWipeout: true, deleteDirs: true
                        }
                    }
                }
            }
            post() {
                success {
                    node('Jenkins-Agent-al2-x64-c54xlarge-Docker-Host') {
                        publishNotification(":white_check_mark:", "Successful Build", "\n${getAllJenkinsMessages()}")
                    }
                }
                failure {
                    node('Jenkins-Agent-al2-x64-c54xlarge-Docker-Host') {
                        publishNotification(":warning:", "Failed Build", "")
                    }
                }
            }
        }
    }
}

void build() {
    git url: 'https://github.com/opensearch-project/opensearch-build.git', branch: 'main'

    sh "./build.sh manifests/$INPUT_MANIFEST"
    sh './assemble.sh artifacts/manifest.yml'

    script { manifest = readYaml(file: 'artifacts/manifest.yml') }
    def artifactPath = "${manifest.build.version}/${BUILD_NUMBER}/${manifest.build.architecture}";

    withAWS(role: 'opensearch-bundle', roleAccount: "${AWS_ACCOUNT_PUBLIC}", duration: 900, roleSessionName: 'jenkins-session') {
        s3Upload(file: 'artifacts', bucket: "${ARTIFACT_BUCKET_NAME}", path: "builds/opensearch-dashboards/${artifactPath}")
        s3Upload(file: "bundle", bucket: "${ARTIFACT_BUCKET_NAME}", path: "bundles/opensearch-dashboards/${artifactPath}")
    }

    addJenkinsMessage("${PUBLIC_ARTIFACT_URL}/builds/opensearch-dashboards/${artifactPath}/manifest.yml\n" +
                      "${PUBLIC_ARTIFACT_URL}/bundles/opensearch-dashboards/${artifactPath}/manifest.yml")
}

/** Publishes a notification to a slack instance*/
void publishNotification(icon, msg, extra) {
    withCredentials([string(credentialsId: 'BUILD_NOTICE_WEBHOOK', variable: 'TOKEN')]) {
        sh("""curl -XPOST --header "Content-Type: application/json" --data '{"result_text": "$icon ${env.JOB_NAME} [${env.BUILD_NUMBER}] $msg ${env.BUILD_URL}\nManifest: ${INPUT_MANIFEST} $extra"}' """ + "$TOKEN")
    }
}

/** Add a message to the jenkins queue */
void addJenkinsMessage(message) {
    writeFile(file: "notifications/${STAGE_NAME}.msg", text: message)
    stash(includes: "notifications/*" , name: "notifications-${STAGE_NAME}")
}

/** Load all message in the jenkins queue and append them with a leading newline into a mutli-line string */
String getAllJenkinsMessages() {
    script {
        // Stages must be explicitly added to prevent overwriting
        // See https://ryan.himmelwright.net/post/jenkins-parallel-stashing/
        def stages = ['build-x64']
        for (stage in stages) {
            unstash "notifications-${stage}"
        }

        def files = findFiles(excludes: '', glob: 'notifications/*')
        def data = ""
        for (file in files) {
            data = data + "\n" + readFile (file: file.path)
        }

        // Delete all the notifications from the workspace
        dir('notifications') {
            deleteDir()
        }
        return data
    }
}
