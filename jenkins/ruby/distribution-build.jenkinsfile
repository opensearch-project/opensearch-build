lib = library(identifier: 'jenkins@20211123', retriever: legacySCM(scm))

pipeline {
    options {
        timeout(time: 1, unit: 'HOURS')
    }
    agent none
    parameters {
        string(
            name: 'REPOSITORY_NAME',
            description: 'Repository to release, e.g. opensearch-ruby',
            trim: true
        )
        string(
            name: 'RELEASE_BRANCH',
            description: 'Branch name to release, e.g. 1.2.0',
            trim: true
        )
        string(
            name: 'RUBY_INTERPRETER',
            description: 'ruby, jruby, etc',
            defaultValue: 'ruby-2.7.0',
            trim: true
        )
    }
    stages {
        stage('ruby-build-sign-upload') {
            agent {
                docker {
                    label 'Jenkins-Agent-al2-x64-c54xlarge-Docker-Host'
                    image 'opensearchstaging/ci-runner:centos7-x64-arm64-jdkmulti-node10.24.1-cypress6.9.1-20211130'
                    args '-u root' // Needed for installing RUBY_INTERPRETER 
                    alwaysPull true
                }
            }
            steps {
                script {
                    git url: "https://github.com/opensearch-project/${REPOSITORY_NAME}.git", branch: "$RELEASE_BRANCH"
                    withCredentials([string(credentialsId: 'opensearchproject-rubygems-private-key', variable: 'RUBYGEMS_PRIVATE_KEY')]) {
                        writeFile file: 'gem-private_key.pem', text: RUBYGEMS_PRIVATE_KEY
                    }
                    sh'''
                        [[ -s "/usr/local/rvm/scripts/rvm" ]] && source "/usr/local/rvm/scripts/rvm"
                        rvm install $RUBY_INTERPRETER
                        rvm get stable --auto-dotfiles
                        . /usr/local/rvm/gems/$RUBY_INTERPRETER/environment
                        gem cert --add certs/opensearch-rubygems.pem
                        ./build.sh
                    '''
                    artifacts_path = ([
                        "${JOB_NAME}",
                        "${REPOSITORY_NAME}",
                        "${RELEASE_BRANCH}",
                        "${BUILD_NUMBER}",
                        "builds"
                    ].join('/'))
                    withAWS(role: 'opensearch-bundle', roleAccount: "${AWS_ACCOUNT_PUBLIC}", duration: 900, roleSessionName: 'jenkins-session') {
                        s3Upload(file: 'builds', bucket: "${ARTIFACT_BUCKET_NAME}", path: "${artifacts_path}")
                    }
                    lib.jenkins.Messages.new(this).add("${STAGE_NAME}", "Built artifacts into ${PUBLIC_ARTIFACT_URL}/${artifacts_path}.")
                    sh """
                        rm -rf $WORKSPACE/*
                    """
                }
            }
            post {
                always {
                    postCleanup()
                }
            }
        }
    }
    post {
        success {
            node('Jenkins-Agent-al2-x64-c54xlarge-Docker-Host') {
                script {
                    def stashed = lib.jenkins.Messages.new(this).get(['ruby-build-sign-upload'])

                    publishNotification(
                        icon: ':white_check_mark:',
                        message: 'Successful Build',
                        credentialsId: 'BUILD_NOTICE_WEBHOOK',
                        extra: stashed,
                    )

                    postCleanup()
                }
            }
        }
        failure {
            node('Jenkins-Agent-al2-x64-c54xlarge-Docker-Host') {
                script {
                    publishNotification(
                        icon: ':warning:',
                        message: 'Failed Build',
                        credentialsId: 'BUILD_NOTICE_WEBHOOK',
                    )

                    postCleanup()
                }
            }
        }
    }
}
