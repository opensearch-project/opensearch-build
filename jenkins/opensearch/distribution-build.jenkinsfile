lib = library(identifier: "jenkins@20211123", retriever: legacySCM(scm))

pipeline {
    agent none
    environment {
        AGENT_X64 = 'Jenkins-Agent-al2-x64-c54xlarge-Docker-Host'
        AGENT_ARM64 = 'Jenkins-Agent-al2-arm64-c6g4xlarge-Docker-Host'
        INTEG_TEST_JOB_NAME = 'integ-test'
    }
    parameters {
        string(
            name: 'INPUT_MANIFEST',
            description: 'Input manifest under the manifests folder, e.g. 2.0.0/opensearch-2.0.0.yml.',
            trim: true
        )
        string(
            name: 'TEST_MANIFEST',
            description: 'Test manifest under the manifests folder, e.g. 2.0.0/opensearch-2.0.0-test.yml.',
            trim: true
        )
    }
    stages {
        stage('detect docker image + args') {
            agent {
                docker {
                    label 'Jenkins-Agent-al2-x64-c54xlarge-Docker-Host'
                    image 'opensearchstaging/ci-runner:centos7-x64-arm64-jdkmulti-node10.24.1-cypress6.9.1-20211028'
                    alwaysPull true
                }
            }
            steps {
                script {
                    dockerAgent = detectDockerAgent()
                    currentBuild.description = INPUT_MANIFEST
                }
            }
        }
        stage('build') {
            parallel {
                stage('build-snapshot-x64') {
                    environment {
                        SNAPSHOT_REPO_URL = "https://aws.oss.sonatype.org/content/repositories/snapshots/"
                    }
                    agent {
                        docker {
                            label 'Jenkins-Agent-al2-x64-c54xlarge-Docker-Host'
                            image dockerAgent.image
                            args dockerAgent.args
                            alwaysPull true
                        }
                    }
                    steps {
                        script {
                            git url: 'https://github.com/opensearch-project/opensearch-build.git', branch: 'main'
                            buildManifest(
                                inputManifest: "manifests/${INPUT_MANIFEST}",
                                snapshot: true
                            )
                            if (fileExists("$WORKSPACE/builds/opensearch/maven")) {
                                withCredentials([usernamePassword(credentialsId: 'Sonatype', usernameVariable: 'SONATYPE_USERNAME', passwordVariable: 'SONATYPE_PASSWORD')]) {
                                    sh('$WORKSPACE/publish/publish-snapshot.sh $WORKSPACE/builds/opensearch/maven')
                                }
                            } else {
                                echo "Skipping publishing snapshots, builds/opensearch/maven does not exist."
                            }
                            echo("Uploading min snapshots to S3")
                            uploadMinSnapshotsToS3(
                                fileActions: [createSha512Checksums()]
                            )
                        }
                    }
                    post {
                        always {
                            postCleanup()
                        }
                    }
                }
                stage('build-snapshot-arm64') {
                    agent {
                        docker {
                            label 'Jenkins-Agent-al2-arm64-c6g4xlarge-Docker-Host'
                            image dockerAgent.image
                            args dockerAgent.args
                            alwaysPull true
                        }
                    }
                    steps {
                        script {
                            git url: 'https://github.com/opensearch-project/opensearch-build.git', branch: 'main'
                            buildManifest(
                                inputManifest: "manifests/${INPUT_MANIFEST}",
                                snapshot: true
                            )
                            echo("Uploading min snapshots to S3")
                            uploadMinSnapshotsToS3(
                                fileActions: [createSha512Checksums()]
                            )
                        }
                    }
                    post {
                        always {
                            postCleanup()
                        }
                    }
                }
                stage('build-and-test-x64') {
                    agent {
                        docker {
                            label AGENT_X64
                            image dockerAgent.image
                            args dockerAgent.args
                            alwaysPull true
                        }
                    }
                    steps {
                        script {
                            def buildManifestObj = buildAssembleUpload(
                                inputManifest: "manifests/${INPUT_MANIFEST}"
                            )
                            String buildManifestUrl = buildManifestObj.getUrl(JOB_NAME, BUILD_NUMBER)
                            String artifactUrl = buildManifestObj.getArtifactUrl(JOB_NAME, BUILD_NUMBER)
                            env.ARTIFACT_URL_X64 = artifactUrl

                            echo "buildManifestUrl (x64): ${buildManifestUrl}"
                            echo "artifactUrl (x64): ${artifactUrl}"

                            def integTestResults = 
                                build job: INTEG_TEST_JOB_NAME,
                                propagate: false,
                                wait: true,
                                parameters: [
                                    string(name: 'TEST_MANIFEST', value: TEST_MANIFEST),
                                    string(name: 'BUILD_MANIFEST_URL', value: buildManifestUrl),
                                    string(name: 'AGENT_LABEL', value: AGENT_X64)
                                ]

                            String status = integTestResults.getResult()
                            String icon = status == 'SUCCESS' ? ':white_check_mark:' : ':warning:'
                            lib.jenkins.Messages.new(this).add(
                                STAGE_NAME,
                                lib.jenkins.Messages.new(this).get([STAGE_NAME]) + 
                                    "\nInteg Tests (x64): ${icon} ${status} ${integTestResults.getAbsoluteUrl()}"
                            )
                        }
                    }
                    post {
                        always {
                            postCleanup()
                        }
                    }
                }
                stage('build-and-test-arm64') {
                    agent {
                        docker {
                            label AGENT_ARM64
                            image dockerAgent.image
                            args dockerAgent.args
                            alwaysPull true
                        }
                    }
                    steps {
                        script {
                            def buildManifestObj = buildAssembleUpload(
                                inputManifest: "manifests/${INPUT_MANIFEST}"
                            )
                            String buildManifestUrl = buildManifestObj.getUrl(JOB_NAME, BUILD_NUMBER)
                            String artifactUrl = buildManifestObj.getArtifactUrl(JOB_NAME, BUILD_NUMBER)
                            env.ARTIFACT_URL_ARM64 = artifactUrl

                            echo "buildManifestUrl (arm64): ${buildManifestUrl}"
                            echo "artifactUrl (arm64): ${artifactUrl}"

                            def integTestResults = 
                                build job: INTEG_TEST_JOB_NAME,
                                propagate: false,
                                wait: true,
                                parameters: [
                                    string(name: 'TEST_MANIFEST', value: TEST_MANIFEST),
                                    string(name: 'BUILD_MANIFEST_URL', value: buildManifestUrl),
                                    string(name: 'AGENT_LABEL', value: AGENT_ARM64)
                                ]

                            String status = integTestResults.getResult()
                            String icon = status == 'SUCCESS' ? ':white_check_mark:' : ':warning:'
                            lib.jenkins.Messages.new(this).add(
                                STAGE_NAME,
                                lib.jenkins.Messages.new(this).get([STAGE_NAME]) + 
                                    "\nInteg Tests (arm64): ${icon} ${status} ${integTestResults.getAbsoluteUrl()}"
                            )
                        }
                    }
                    post {
                        always {
                            postCleanup()
                        }
                    }
                }
            }
        }
        stage('docker build') {
            steps {
                node('Jenkins-Agent-al2-x64-c54xlarge-Docker-Host') {
                    script {

                        echo "env.ARTIFACT_URL_X64: ${env.ARTIFACT_URL_X64}"
                        echo "env.ARTIFACT_URL_ARM64: ${env.ARTIFACT_URL_ARM64}"
                        
                        buildDockerImage(
                            inputManifest: "manifests/${INPUT_MANIFEST}",
                            artifactUrlX64: env.ARTIFACT_URL_X64,
                            artifactUrlArm64: env.ARTIFACT_URL_ARM64
                        )
                    }
                }
            }
        }
    }
    post {
        success {
            node(AGENT_X64) {
                script {
                    def stashed = lib.jenkins.Messages.new(this).get(['build-and-test-x64', 'build-and-test-arm64'])

                    publishNotification(
                        icon: ':white_check_mark:',
                        message: 'Successful Build',
                        extra: stashed,
                        credentialsId: 'BUILD_NOTICE_WEBHOOK',
                        manifest: "${INPUT_MANIFEST}"
                    )

                    postCleanup()
                }
            }
        }
        failure {
            node(AGENT_X64) {
                script {
                    publishNotification(
                        icon: ':warning:',
                        message: 'Failed Build',
                        credentialsId: 'BUILD_NOTICE_WEBHOOK',
                        manifest: "${INPUT_MANIFEST}"
                    )

                    postCleanup()
                }
            }
        }
    }
}
