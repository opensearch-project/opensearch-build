/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 */

/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.5/userguide/tutorial_java_projects.html
 */

plugins {
    id 'com.mkobit.jenkins.pipelines.shared-library' version '0.10.1'
    id 'java'
    id 'groovy'
    id 'jacoco'
}

repositories {
    maven { url 'https://repo.jenkins-ci.org/releases/' }
    maven { url 'https://mvnrepository.com/artifact/' }
    mavenLocal()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.9', ext: 'pom'
    implementation group: 'com.cloudbees', name: 'groovy-cps', version: '1.31'
    testImplementation group: 'org.yaml', name: 'snakeyaml', version: '1.33'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.4.1'
    testImplementation group: 'com.lesfurets', name:'jenkins-pipeline-unit', version: '1.13'
}

configurations.all {
    resolutionStrategy {
        force group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
        force group: 'commons-codec', name: 'commons-codec', version: '1.15'
        force group: 'com.google.protobuf', name: 'protobuf-java', version: '3.21.7'
    }
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/jenkins']
        }
    }

    test {
        groovy {
            srcDirs = ['tests/jenkins']
        }
    }

}

sharedLibrary {
    coreVersion = '2.378' // https://repo.jenkins-ci.org/public/org/jenkins-ci/main/jenkins-core/
    testHarnessVersion = '1736.vc72c458c5103' // https://mvnrepository.com/artifact/org.jenkins-ci.main/jenkins-test-harness?repo=jenkins-releases
    pluginDependencies {
        // see https://mvnrepository.com/artifact/org.jenkins-ci.plugins/<name>?repo=jenkins-releases for latest
        dependency('io.jenkins.plugins', 'pipeline-groovy-lib', '613.v9c41a_160233f')
        dependency('org.jenkins-ci.plugins.workflow', 'workflow-cps', '2803.v1a_f77ffcc773')
        dependency('org.jenkins-ci.plugins.workflow', 'workflow-multibranch', '2.26.1')
        dependency('org.jenkins-ci.plugins', 'pipeline-input-step', '456.vd8a_957db_5b_e9') // https://repo.jenkins-ci.org/public/org/jenkins-ci/plugins/pipeline-input-step/
        dependency('org.jenkins-ci.plugins', 'script-security', '1184.v85d16b_d851b_3')
        dependency('org.jenkins-ci.plugins', 'credentials', '1112.vc87b_7a_3597f6')
        dependency('org.jenkins-ci.plugins', 'git-client', '3.11.1')
        dependency('org.jenkins-ci.plugins', 'junit', '1159.v0b_396e1e07dd')
        dependency('org.jenkins-ci.plugins', 'mailer', '408.vd726a_1130320') // https://repo.jenkins-ci.org/public/org/jenkins-ci/plugins/mailer/
    }
}

test {
    testLogging {
		events "failed"
		exceptionFormat "full"
    }
    if (project.hasProperty('pipeline.stack.write')) {
        systemProperty 'pipeline.stack.write', project.getProperty('pipeline.stack.write')
    }

    jacoco {
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }

    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    dependsOn test

    sourceSets sourceSets.main

    afterEvaluate {
        classDirectories.from = fileTree(
            dir: "$buildDir/jacoco/classpathdumps",
            includes: [
                '**/*_Jenkinsfile.*',
                '**/jenkins/*'
            ],
            excludes: [
                '**/*\$_get_closure*'
            ]
        )
    }

    reports {
        xml.required = true
    }
}
