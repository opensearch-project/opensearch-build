/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 */

/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.5/userguide/tutorial_java_projects.html
 */

plugins {
    id 'com.mkobit.jenkins.pipelines.shared-library' version '0.10.1'
    id 'java'
    id 'groovy'
    id 'jacoco'
}

repositories {
    maven { url 'https://repo.jenkins-ci.org/releases/' }
    maven { url 'https://mvnrepository.com/artifact/' }
    mavenLocal()
}

dependencies {
    compile group: 'junit', name: 'junit', version: '4.13.2'
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.9', ext: 'pom'
    implementation group: 'com.cloudbees', name: 'groovy-cps', version: '1.31'
    testImplementation group: 'org.yaml', name: 'snakeyaml', version: '1.29'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.4.1'
    testImplementation group: 'com.lesfurets', name:'jenkins-pipeline-unit', version: '1.13'
}

configurations.all {
    resolutionStrategy {
        force group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
        force group: 'commons-codec', name: 'commons-codec', version: '1.15'
        force group: 'com.google.protobuf', name: 'protobuf-java', version: '3.19.3'
    }
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/jenkins']
        }
    }

    test {
        groovy {
            srcDirs = ['tests/jenkins']
        }
    }

    jobs {
        groovy {
            srcDirs 'src/jenkins/jobs'
            compileClasspath += main.compileClasspath
        }

        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

sharedLibrary {
    coreVersion = '2.334' // https://mvnrepository.com/artifact/org.jenkins-ci.main/jenkins-core?repo=jenkins-releases
    testHarnessVersion = '2.72' // https://mvnrepository.com/artifact/org.jenkins-ci.main/jenkins-test-harness?repo=jenkins-releases
    pluginDependencies {
        workflowCpsGlobalLibraryPluginVersion = '2.21.1' // https://mvnrepository.com/artifact/org.jenkins-ci.plugins.workflow/workflow-cps-global-lib?repo=jenkins-releases
        // see https://mvnrepository.com/artifact/org.jenkins-ci.plugins/<name>?repo=jenkins-releases for latest
        dependency('org.jenkins-ci.plugins.workflow', 'workflow-cps', '2.94.1')
        dependency('org.jenkins-ci.plugins.workflow', 'workflow-multibranch', '2.26.1')
        dependency('org.jenkins-ci.plugins', 'pipeline-input-step', '2.12')
        dependency('org.jenkins-ci.plugins', 'script-security', '1.78')
        dependency('org.jenkins-ci.plugins', 'credentials', '2.6.2')
        dependency('org.jenkins-ci.plugins', 'git-client', '3.10.1')
        dependency('org.jenkins-ci.plugins', 'junit', '1.55')
        dependency('org.jenkins-ci.plugins', 'mailer', '1.34.2')
    }
}

test {
    testLogging {
		events "failed"
		exceptionFormat "full"
    }
    if (project.hasProperty('pipeline.stack.write')) {
        systemProperty 'pipeline.stack.write', project.getProperty('pipeline.stack.write')
    }

    jacoco {
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }

    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    dependsOn test

    sourceSets sourceSets.main

    afterEvaluate {
        classDirectories.from = fileTree(
            dir: "$buildDir/jacoco/classpathdumps",
            includes: [
                '**/*_Jenkinsfile.*',
                '**/jenkins/*'
            ],
            excludes: [
                '**/*\$_get_closure*'
            ]
        )
    }

    reports {
        xml.required = true
    }
}
